plugins {
    id 'com.android.library'
}
apply plugin: 'com.github.dcendents.android-maven'    // 添加本行
group='com.github.shengwang520'    // 添加本行

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 5
        versionName "1.0.05"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    api 'com.orhanobut:logger:1.4'//打印数据框架

    api 'com.google.code.gson:gson:2.8.6'
    api 'com.github.chrisbanes.photoview:library:1.2.4'//图片手势放大缩小

    api 'com.karumi:dexter:6.2.2'//6.0+权限管理

    api 'com.jude:easyrecyclerview:4.4.2' //列表的扩展
    api 'org.apache.httpcomponents:httpcore:4.4.10'

    //图片加载库
    api 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    api 'com.github.yalantis:ucrop:2.2.6'//图片裁切

    api 'com.squareup.okhttp3:okhttp:4.2.1'
    //RxJava库的引用
    api 'io.reactivex.rxjava2:rxandroid:2.0.1'
    api 'io.reactivex.rxjava2:rxjava:2.2.2'

}

// 指定编码
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}
// 打包源码
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}
// 制作文档(Javadoc)
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar
    archives javadocJar
}
